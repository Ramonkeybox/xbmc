From 20cfc56cb50f670fbc4b43d500c136f8a8422b3e Mon Sep 17 00:00:00 2001
From: Janne Grunau <janne-libav@jannau.net>
Date: Sun, 20 Apr 2014 17:57:36 +0200
Subject: [PATCH 119/125] aarch64: NEON vorbis_inverse_coupling

From the ARMv7 NEON version. 16 times faster as the C version, overall
more than 12% faster vorbis decoding on Apple's A7.

Conflicts:
	libavcodec/vorbisdsp.c

Signed-off-by: Linus Yang <laokongzi@gmail.com>
---
 libavcodec/aarch64/Makefile         |  3 ++
 libavcodec/aarch64/vorbisdsp_init.c | 34 +++++++++++++++
 libavcodec/aarch64/vorbisdsp_neon.S | 82 +++++++++++++++++++++++++++++++++++++
 libavcodec/vorbisdsp.c              |  2 +
 libavcodec/vorbisdsp.h              |  1 +
 5 files changed, 122 insertions(+)
 create mode 100644 libavcodec/aarch64/vorbisdsp_init.c
 create mode 100644 libavcodec/aarch64/vorbisdsp_neon.S

diff --git a/libavcodec/aarch64/Makefile b/libavcodec/aarch64/Makefile
index cc23864..13a2432 100644
--- a/libavcodec/aarch64/Makefile
+++ b/libavcodec/aarch64/Makefile
@@ -7,6 +7,7 @@ OBJS-$(CONFIG_MPEGAUDIODSP)             += aarch64/mpegaudiodsp_init.o
 OBJS-$(CONFIG_VIDEODSP)                 += aarch64/videodsp_init.o
 OBJS-$(CONFIG_RV40_DECODER)             += aarch64/rv40dsp_init_aarch64.o
 OBJS-$(CONFIG_VC1_DECODER)              += aarch64/vc1dsp_init_aarch64.o
+OBJS-$(CONFIG_VORBIS_DECODER)           += aarch64/vorbisdsp_init.o
 
 ARMV8-OBJS-$(CONFIG_VIDEODSP)           += aarch64/videodsp.o
 
@@ -19,3 +20,5 @@ NEON-OBJS-$(CONFIG_H264QPEL)            += aarch64/h264qpel_neon.o             \
 NEON-OBJS                               += aarch64/hpeldsp_neon.o
 NEON-OBJS-$(CONFIG_MPEGAUDIODSP)        += aarch64/mpegaudiodsp_neon.o
 NEON-OBJS-$(CONFIG_MDCT)                += aarch64/mdct_neon.o
+
+NEON-OBJS-$(CONFIG_VORBIS_DECODER)      += aarch64/vorbisdsp_neon.o
diff --git a/libavcodec/aarch64/vorbisdsp_init.c b/libavcodec/aarch64/vorbisdsp_init.c
new file mode 100644
index 0000000..3559b54
--- /dev/null
+++ b/libavcodec/aarch64/vorbisdsp_init.c
@@ -0,0 +1,34 @@
+/*
+ * This file is part of Libav.
+ *
+ * Libav is free software; you can redistribute it and/or
+ * modify it under the terms of the GNU Lesser General Public
+ * License as published by the Free Software Foundation; either
+ * version 2.1 of the License, or (at your option) any later version.
+ *
+ * Libav is distributed in the hope that it will be useful,
+ * but WITHOUT ANY WARRANTY; without even the implied warranty of
+ * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
+ * Lesser General Public License for more details.
+ *
+ * You should have received a copy of the GNU Lesser General Public
+ * License along with Libav; if not, write to the Free Software
+ * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
+ */
+
+#include "libavutil/attributes.h"
+#include "libavutil/cpu.h"
+#include "libavutil/aarch64/cpu.h"
+#include "libavcodec/vorbisdsp.h"
+
+void ff_vorbis_inverse_coupling_neon(float *mag, float *ang,
+                                     intptr_t blocksize);
+
+av_cold void ff_vorbisdsp_init_aarch64(VorbisDSPContext *c)
+{
+    int cpu_flags = av_get_cpu_flags();
+
+    if (have_neon(cpu_flags)) {
+        c->vorbis_inverse_coupling = ff_vorbis_inverse_coupling_neon;
+    }
+}
diff --git a/libavcodec/aarch64/vorbisdsp_neon.S b/libavcodec/aarch64/vorbisdsp_neon.S
new file mode 100644
index 0000000..11f71f1
--- /dev/null
+++ b/libavcodec/aarch64/vorbisdsp_neon.S
@@ -0,0 +1,82 @@
+/*
+ * Copyright (c) 2008 Mans Rullgard <mans@mansr.com>
+ *
+ * This file is part of Libav.
+ *
+ * Libav is free software; you can redistribute it and/or
+ * modify it under the terms of the GNU Lesser General Public
+ * License as published by the Free Software Foundation; either
+ * version 2.1 of the License, or (at your option) any later version.
+ *
+ * Libav is distributed in the hope that it will be useful,
+ * but WITHOUT ANY WARRANTY; without even the implied warranty of
+ * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
+ * Lesser General Public License for more details.
+ *
+ * You should have received a copy of the GNU Lesser General Public
+ * License along with Libav; if not, write to the Free Software
+ * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
+ */
+
+#include "libavutil/aarch64/asm.S"
+
+function ff_vorbis_inverse_coupling_neon, export=1
+        movi            v20.4s,  #1<<7, lsl #24
+        subs            x2,  x2,  #4
+        mov             x3,  x0
+        mov             x4,  x1
+        b.eq            3f
+
+        ld1             {v7.4s},  [x1], #16
+        ld1             {v6.4s},  [x0], #16
+        cmle            v4.4s,  v7.4s,  #0
+        and             v5.16b, v6.16b, v20.16b
+        eor             v7.16b, v7.16b, v5.16b
+        and             v2.16b, v7.16b, v4.16b
+        bic             v3.16b, v7.16b, v4.16b
+        fadd            v7.4s,  v6.4s,  v2.4s
+        fsub            v6.4s,  v6.4s,  v3.4s
+1:      ld1             {v1.4s},  [x1], #16
+        ld1             {v0.4s},  [x0], #16
+        cmle            v4.4s,  v1.4s,  #0
+        and             v5.16b, v0.16b, v20.16b
+        eor             v1.16b, v1.16b, v5.16b
+        st1             {v7.4s},  [x3], #16
+        st1             {v6.4s},  [x4], #16
+        and             v2.16b, v1.16b, v4.16b
+        bic             v3.16b, v1.16b, v4.16b
+        fadd            v1.4s,  v0.4s,  v2.4s
+        fsub            v0.4s,  v0.4s,  v3.4s
+        subs            x2,  x2,  #8
+        b.le            2f
+        ld1             {v7.4s},  [x1], #16
+        ld1             {v6.4s},  [x0], #16
+        cmle            v4.4s,  v7.4s,  #0
+        and             v5.16b, v6.16b, v20.16b
+        eor             v7.16b, v7.16b, v5.16b
+        st1             {v1.4s},  [x3], #16
+        st1             {v0.4s},  [x4], #16
+        and             v2.16b, v7.16b, v4.16b
+        bic             v3.16b, v7.16b, v4.16b
+        fadd            v7.4s,  v6.4s,  v2.4s
+        fsub            v6.4s,  v6.4s,  v3.4s
+        b               1b
+
+2:      st1             {v1.4s},  [x3], #16
+        st1             {v0.4s},  [x4], #16
+        b.lt            ret
+
+3:      ld1             {v1.4s},  [x1]
+        ld1             {v0.4s},  [x0]
+        cmle            v4.4s,  v1.4s,  #0
+        and             v5.16b, v0.16b, v20.16b
+        eor             v1.16b, v1.16b, v5.16b
+        and             v2.16b, v1.16b, v4.16b
+        bic             v3.16b, v1.16b, v4.16b
+        fadd            v1.4s,  v0.4s,  v2.4s
+        fsub            v0.4s,  v0.4s,  v3.4s
+        st1             {v1.4s},  [x0], #16
+        st1             {v0.4s},  [x1], #16
+ret:
+        ret
+endfunc
diff --git a/libavcodec/vorbisdsp.c b/libavcodec/vorbisdsp.c
index fd8dcd7..c56e22a 100644
--- a/libavcodec/vorbisdsp.c
+++ b/libavcodec/vorbisdsp.c
@@ -24,6 +24,8 @@ void ff_vorbisdsp_init(VorbisDSPContext *dsp)
 {
     dsp->vorbis_inverse_coupling = ff_vorbis_inverse_coupling;
 
+    if (ARCH_AARCH64)
+        ff_vorbisdsp_init_aarch64(dsp);
     if (ARCH_X86)
         ff_vorbisdsp_init_x86(dsp);
     if (ARCH_PPC)
diff --git a/libavcodec/vorbisdsp.h b/libavcodec/vorbisdsp.h
index ed14049..7abec4e 100644
--- a/libavcodec/vorbisdsp.h
+++ b/libavcodec/vorbisdsp.h
@@ -30,6 +30,7 @@ typedef struct VorbisDSPContext {
 void ff_vorbisdsp_init(VorbisDSPContext *dsp);
 
 /* for internal use only */
+void ff_vorbisdsp_init_aarch64(VorbisDSPContext *dsp);
 void ff_vorbisdsp_init_x86(VorbisDSPContext *dsp);
 void ff_vorbisdsp_init_arm(VorbisDSPContext *dsp);
 void ff_vorbisdsp_init_ppc(VorbisDSPContext *dsp);
-- 
1.9.3

